import java.util.Properties

plugins {
    alias(libs.plugins.com.android.application)
    alias(libs.plugins.org.jetbrains.kotlin.android)
    alias(libs.plugins.org.jetbrains.kotlin.plugin.compose)
}

android {
    namespace 'com.m3u.plugin'
    compileSdk 35
    

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a'
            universalApk true
        }
    }

    defaultConfig {
        applicationId "com.m3u.plugin"
        minSdk 26
        targetSdk 35
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        def rootPathEscaped = rootProject.projectDir.absolutePath.replace("\\", "\\\\")
        buildConfigField "String", "PROJECT_ROOT_PATH", "\"${rootPathEscaped}\""
        
        buildConfigField "String", "DROPBOX_APP_KEY", "\"f6nypo6r87ltw7i\""
        buildConfigField "String", "DROPBOX_APP_SECRET", "\"ppgw0myjxdxn7fl\""
        buildConfigField "String", "DROPBOX_REFRESH_TOKEN", "\"UASt2SANM74AAAAAAAAAATytQgDha2NsamfnZML4Cd-Mtwg5bEzmt8fMHwnGxInf\""
 
        ndk {
            abiFilters 'arm64-v8a'
        }
        ndkVersion project.property("ndkVersion")
    }


    signingConfigs {
        release {
            Properties localProperties = new Properties()
            File localPropertiesFile = rootProject.file("local.properties")
            if (localPropertiesFile.exists()) {
                localPropertiesFile.withInputStream { localProperties.load(it) }
            }

            // Prioritiza Environment Variables (CI) > local.properties > Default
            def storeFilePath = System.getenv("RELEASE_STORE_FILE") ?: localProperties.getProperty("RELEASE_STORE_FILE") ?: "meu-app.keystore"
            def storeFileObj = new File(storeFilePath).isAbsolute() ? new File(storeFilePath) : rootProject.file(storeFilePath)

            if (storeFileObj.exists()) {
                storeFile storeFileObj
                storePassword System.getenv("RELEASE_STORE_PASSWORD") ?: localProperties.getProperty("RELEASE_STORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEY_ALIAS") ?: localProperties.getProperty("RELEASE_KEY_ALIAS")
                keyPassword System.getenv("RELEASE_KEY_PASSWORD") ?: localProperties.getProperty("RELEASE_KEY_PASSWORD")
            } else {
                File userDebug = file(System.getProperty("user.home") + "/.android/debug.keystore")
                if (userDebug.exists()) {
                    storeFile userDebug
                    storePassword "android"
                    keyAlias "androiddebugkey"
                    keyPassword "android"
                }
            }
        }
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
            keepDebugSymbols += "**/*.so"
        }
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/DEPENDENCIES'
            excludes += 'META-INF/INDEX.LIST'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    
    kotlinOptions {
        jvmTarget = '21'
    }
    
    buildFeatures {
        aidl true
        compose true
        buildConfig true
    }

    lint {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation project(":core:aidl")
    implementation project(":core:foundation")
    implementation libs.androidx.core.ktx
    implementation "androidx.core:core:1.15.0"
    implementation libs.androidx.appcompat
    implementation libs.google.material
    
    // Compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.material3
    implementation libs.androidx.activity.compose
    implementation libs.androidx.compose.ui.util
    implementation libs.androidx.compose.material.icons.extended

    // Design Premium (Glassmorphism)
    implementation libs.haze
    implementation libs.haze.materials

    // NewPipe Extractor (Native YouTube Extraction)
    implementation libs.newpipe.extractor

    implementation libs.okhttp
    implementation libs.jsoup

    // DataStore
    implementation libs.androidx.datastore.preferences
 
    // Network
    implementation libs.okhttp
    implementation libs.gson
    implementation libs.dropbox.core.sdk
    
    // Coroutines
    implementation libs.kotlinx.coroutines.android

    // WorkManager
    implementation libs.androidx.work.runtime.ktx
 
    // Testing
    testImplementation "junit:junit:4.13.2"
    testImplementation libs.kotlinx.coroutines.android // Exemplo, deveria ser test ktx
}
